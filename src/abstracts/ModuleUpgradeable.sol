// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity 0.8.28;

import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {Initializable} from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import {IFlatcoinVault} from "../interfaces/IFlatcoinVault.sol";
import {ICommonErrors} from "../interfaces/ICommonErrors.sol";

/// @title ModuleUpgradeable
/// @author dHEDGE
/// @notice This is the base contract for all upgradeable modules in the Flatcoin system.
abstract contract ModuleUpgradeable is Initializable {
    //////////////////////////////////////////
    //               Errors                 //
    //////////////////////////////////////////

    error Paused(bytes32 moduleKey);
    error OnlyOwner(address msgSender);
    error ModuleKeyEmpty();

    //////////////////////////////////////////
    //                State                 //
    //////////////////////////////////////////

    /// @notice The bytes32 encoded key of the module.
    /// @dev Note that this shouldn't change ever for existing modules.
    ///      Due to this module being upgradeable, we can't use immutable here.
    // solhint-disable-next-line var-name-mixedcase
    bytes32 public MODULE_KEY;

    /// @notice The FlatcoinVault contract referred to by all modules.
    /// @dev Contains all the module addresses, the state of the system and more.
    IFlatcoinVault public vault;

    //////////////////////////////////////////
    //               Modifiers              //
    //////////////////////////////////////////

    modifier onlyAuthorizedModule() {
        if (vault.isAuthorizedModule(msg.sender) == false) revert ICommonErrors.OnlyAuthorizedModule(msg.sender);
        _;
    }

    modifier whenNotPaused() {
        if (vault.isModulePaused(MODULE_KEY)) revert Paused(MODULE_KEY);
        _;
    }

    modifier onlyOwner() {
        if (OwnableUpgradeable(address(vault)).owner() != msg.sender) revert OnlyOwner(msg.sender);
        _;
    }

    //////////////////////////////////////////
    //               Functions              //
    //////////////////////////////////////////

    /// @notice Setter for the vault contract.
    /// @dev Can be used in case FlatcoinVault ever changes.
    function setVault(IFlatcoinVault vault_) external onlyOwner {
        if (address(vault_) == address(0)) revert ICommonErrors.ZeroAddress("vault");

        vault = vault_;
    }

    /// @dev Function to initialize a module.
    /// @param moduleKey_ The bytes32 encoded key of the module.
    /// @param vault_ FlatcoinVault contract address.
    // solhint-disable-next-line func-name-mixedcase
    function __Module_init(bytes32 moduleKey_, IFlatcoinVault vault_) internal {
        if (moduleKey_ == bytes32("")) revert ModuleKeyEmpty();
        if (address(vault_) == address(0)) revert ICommonErrors.ZeroAddress("vault");

        MODULE_KEY = moduleKey_;
        vault = vault_;
    }

    uint256[48] private __gap;
}
